/* XPECT_SETUP org.uqbar.project.wollok.tests.typesystem.xpect.TypeSystemXpectTestCase END_SETUP */

object lobo {

	var calorias = 100
	
	// XPECT methodType at calorias  --> () => Number
	method calorias(){
		return calorias
	}
	
	// XPECT methodType at estasGordo  --> () => Boolean
	method estasGordo(){
		return calorias > 200
	}
	
	// XPECT methodType at estasSaludable  --> () => Boolean
	method estasSaludable(){
		return calorias > 20 and calorias < 150
	}

	// XPECT methodType at correr  --> (Number) => Void
	method correr(minutos){
		calorias = calorias - minutos * 2
	}
	
	// XPECT! methodType at comer  --> ((abuela|chanchito|chanchitoCasaMadera|chanchitoTrabajador|oveja|caperucita|duende)) => Void
	method comer(personaje){
		calorias = calorias + personaje.caloriasQueAporta()
	}
	
	// XPECT! methodType at correrA  --> ((casaPaja|casaMadera|casaLadrillos|casaAbuela|bosque)) => Void
	method correrA(lugar){
		self.correr(lugar.minutos())
	}

	// XPECT! methodType at soplar  --> ((casaPaja|casaMadera|casaLadrillos)) => Void
	method soplar(casa){
		calorias = calorias - (casa.resistencia() + casa.pesoOcupantes())
	}
		
}

object chanchito{
	method peso(){
		return 50
	}
	// XPECT methodType at caloriasQueAporta  --> () => Number
	method caloriasQueAporta(){
		return self.peso() / 10
	}
}


object chanchitoCasaMadera{
	method peso(){
		return 20
	}
// XPECT methodType at caloriasQueAporta  --> () => Integer
	method caloriasQueAporta(){
		return 7
	}
}

object chanchitoTrabajador{
	var edad = 2
	
	method peso(){
		return edad * 5
	}
// XPECT methodType at caloriasQueAporta  --> () => Number
// deberia ser Integer
	method caloriasQueAporta(){
		return self.peso()
	}
}

 
object casaPaja{
// XPECT methodType at pesoOcupantes  --> () => Integer
	method pesoOcupantes(){
		return chanchito.peso()
	}
	
	method resistencia(){
		return 0
	}
	
	method minutos(){
		return 10
	}
}

object casaMadera{
// XPECT type at minutos  --> Integer
	var minutos = 15
// XPECT! type at visitante  --> (chanchito|chanchitoCasaMadera|chanchitoTrabajador|canasta)
	var visitante
//	var visitante = chanchito
	

	method llega(alguien){
		visitante = alguien
	}

	method pesoOcupantes(){
		return chanchitoCasaMadera.peso() + visitante.peso()
	}
	
	method resistencia(){
		return 5
	}
	
	method minutos(){
		return minutos
	}   
}


object casaLadrillos{
	var ladrillos = 10
	var pesoOcupado = chanchitoTrabajador.peso()
//	var pesoOcupado = 10
	
	method llega(chanchito){
		pesoOcupado = pesoOcupado + chanchito.peso()
	}
	
	method pesoOcupantes(){
		return pesoOcupado
	}
	
	method resistencia(){
		return 2 * ladrillos
	}
	
	method minutos(){
		return casaPaja.minutos()+casaMadera.minutos()
	}
}

object abuela{
	
	method caloriasQueAporta(){
		return 0
	}
}

object casaAbuela{
	var minutos = 5
	
	method minutos(){
		return minutos
	}
}

object oveja{
	var nombre = "Dolly"
	
	method caloriasQueAporta(){
		return nombre.size()*5
	}
}
 object duende {
 	var varitas = 0 
 	const altura = 5
 	
 	method encontrarVaritas(cantidad) {
 		varitas = varitas + cantidad
 	}
 
 	method caloriasQueAporta() {
 		return varitas + altura /10
 	}
 	
 }


object bosque{
	method minutos(){
		return 10
	}
}

object canasta {
	var cantidadManzanas = 2
	method peso() {
		return 2 * cantidadManzanas
	} 
}

object caperucita{
	
	method caloriasQueAporta(){
		return 50 + canasta.peso()
	}
	method dondeVas(){
		return casaAbuela
	}
}

